PROG    -> DECL PROG | DECL

DECL    -> param    id : INT ;
DECL    -> curve    id = ADD, t : INT ;
DECL    -> surface  id = ADD, u : INT, v : INT ;
DECL    -> define   id = ADD ;
DECL    -> function id ( ARGS ) = ADD ;
DECL    -> grid     id : INT , ADD ;
DECL    -> point    id = ADD ;
DECL    -> vector   id = ADD @ ADD ;

ARGS    -> id , ARGS | id

INT     -> [ ADD , ADD ]

ADD     -> ADD + MULT
ADD     -> ADD - MULT
ADD     -> MULT

MULT    -> MULT * UNARY
MULT    -> MULT APP
MULT    -> MULT / UNARY
MULT    -> UNARY

UNARY   -> + UNARY
UNARY   -> - UNARY
UNARY   -> * UNARY
UNARY   -> / UNARY
UNARY   -> APP

APP     -> FUNC UNARY | EXP

FUNC    -> FUNC ^ UNARY
FUNC    -> FUNC _ var_id
FUNC    -> FUNC '
FUNC    -> func_id

EXP     -> COMP ^ UNARY | COMP

COMP    -> FACT . number | FACT

FACT    -> const
FACT    -> number
FACT    -> var_id
FACT    -> TUPLE

TUPLE   -> ( LIST )
LIST    -> ADD, LIST | ADD